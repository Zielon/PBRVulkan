#version 460

precision highp float;
precision highp int;

#extension GL_ARB_gpu_shader_int64 : require
#extension GL_ARB_shader_clock : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : require

// Replaced by Compiler.h
// ====== DEFINES ======

#include "../Common/Structs.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT TLAS;
layout(binding = 1, rgba32f) uniform image2D AccumulationImage;
layout(binding = 2, rgba8) uniform image2D OutputImage;
layout(binding = 10, rgba32f) uniform image2D NormalsImage;
layout(binding = 11, rgba32f) uniform image2D PositionImage;
layout(binding = 3) readonly uniform UniformBufferObject { Uniform ubo; };

layout(location = 0) rayPayloadEXT RayPayload payload;

#include "../Common/Random.glsl"
#include "../Common/Math.glsl"

void main() 
{
	mat4 projInv = inverse(ubo.proj);
	mat4 viewInv = inverse(ubo.view);

	uint  rayFlags = gl_RayFlagsOpaqueEXT;
	float tMin     = MINIMUM;
	float tMax     = INFINITY;

	uint SPP = ubo.spp; // Samples per pixel
	seed = tea(gl_LaunchIDEXT.y * gl_LaunchIDEXT.x + gl_LaunchIDEXT.x, ubo.frame);

	vec3 radiance = vec3(0);

	for (uint i = 0; i < SPP; ++i)
	{
		float r1 = rnd(seed);
		float r2 = rnd(seed);

		vec2 jitter     = vec2(r1, r2);
		vec3 lensOffset = vec3(ubo.aperture / 2.0 * sampleUnitDisk(), 0.0);

		vec2 pixel     = vec2(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y) + jitter;
		vec2 uv        = (pixel / gl_LaunchSizeEXT.xy) * 2.0 - 1.0;
		vec4 origin    = viewInv * vec4(lensOffset, 1.0);
		vec4 target    = projInv * vec4(uv.x, uv.y, 1.0, 1.0);
		vec4 direction = viewInv * vec4(normalize(target.xyz * ubo.focalDistance - lensOffset), 0.0);

		Ray ray = Ray(origin.xyz, direction.xyz);

		vec3 beta = vec3(1);
		vec3 pathRadiance = vec3(0);
		bool specularBounce = false;
		BsdfSample bsdf;

		for (uint j = 0; j < ubo.maxDepth; ++j)
		{
			payload.depth = j;
			payload.stop = false;
			payload.radiance = pathRadiance;
			payload.beta = beta;
			payload.ray = ray;
			payload.bsdf = bsdf;
			payload.specularBounce = specularBounce;

			traceRayEXT(
					TLAS,           // acceleration structure
					rayFlags,       // rayFlags
					0xFF,           // cullMask
					0,              // sbtRecordOffset
					0,              // sbtRecordStride
					0,              // missIndex
					ray.origin,     // ray origin
					tMin,           // ray min range
					ray.direction,  // ray direction
					tMax,           // ray max range
					0               // payload (location = 0)
			);

			pathRadiance = payload.radiance;
			beta = payload.beta;
			specularBounce = payload.specularBounce;		
			ray = payload.ray;
			bsdf = payload.bsdf;
			
			// First hit payload. Store infromation for denosing
			if (j == 0)
			{
				imageStore(NormalsImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.ffnormal, 0.0));
				imageStore(PositionImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.worldPos, 0.0));
			}

			if (payload.stop) break;
		}

		radiance += pathRadiance;
	}
	
	radiance /= float(SPP);

	// HDR scale
	vec4 accumulated = ubo.frame > 1 ? imageLoad(AccumulationImage, ivec2(gl_LaunchIDEXT.xy)) : vec4(0.0);
	vec3 accumulatedRadiance = accumulated.xyz + radiance;

	imageStore(AccumulationImage, ivec2(gl_LaunchIDEXT.xy), vec4(accumulatedRadiance, 1.0));

	float inv = 1.f / float(ubo.frame);
	radiance = accumulatedRadiance * inv;

	// LDR scale
	vec3 ldr = toneMap(radiance, 1.5);

	#ifdef USE_GAMMA_CORRECTION
	ldr = gammaCorrection(ldr);
	#endif

	imageStore(OutputImage, ivec2(gl_LaunchIDEXT.xy), vec4(ldr, 1.f));
}
